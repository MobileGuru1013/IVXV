buildscript {
    ext.base = '../../'
    apply from: "${base}/common/java/common-buildscript.gradle", to: buildscript
}

apply from: "${base}/common/java/common-build.gradle"

dependencies {
    compile 'ch.qos.cal10n:cal10n-api:0.7.7'
    compile 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    compile 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.9'
    compile 'org.apache.pdfbox:pdfbox:2.0.6'
    compile 'org.bouncycastle:bcprov-jdk15on:1.55'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.55'
    compile 'org.digidoc4j:digidoc4j:1.0.6'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.yaml:snakeyaml:1.18'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'pl.pragmatists:JUnitParams:1.1.0'
    testCompile 'org.mockito:mockito-core:2.+'
}

sourceSets {
    main {
        java {
            if (project.hasProperty('development')) {
                srcDir file('src/integration-test/java')
            }
        }
        resources {
            srcDir "${projectDir}/translations"
        }
    }
    test {
        java {
            srcDir file('src/integration-test/java')
        }
    }
}

// Dummy for common Makefile.
task installDist

/*
 * Define configuration and artifact 'tests' that:
 * - includes test classes
 * - includes test resources
 * - publishes test dependencies to dependent module
 */

configurations {
    // The 'extendsFrom' ensures test dependencies are inherited
    tests.extendsFrom testRuntime
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

artifacts {
    tests testJar
}
